import org.dm.gradle.plugins.bundle.BundleUtils

version = '1.0.0'

ext {
  bundleSymbolicName = BundleUtils.getBundleSymbolicName(project)
  servletMinVersion = '2.5'
  servletMaxVersion = '3.0'
  staticFilesDir = 'src/main/webapp'
}

repositories {
  mavenCentral()
}

dependencies {
  compile "javax.servlet:servlet-api:$servletMinVersion"
}

jar {
  archiveName = "${bundleSymbolicName}_${version}.jar"
  manifest {
    attributes 'Web-ContextPath': "/$bundleSymbolicName".toString() // force GString to String
  }
}

bundle {
  instruction 'Import-Package', "javax.servlet;version=\"[$servletMinVersion,$servletMaxVersion)\""
  instruction 'Import-Package', "javax.servlet.http;version=\"[$servletMinVersion,$servletMaxVersion)\""
  instruction '-noee', 'true'
  instruction '-wab', staticFilesDir
}

